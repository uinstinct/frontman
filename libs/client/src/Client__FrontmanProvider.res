module Log = FrontmanLogs.Logs.Make({
  let component = #FrontmanProvider
})

module ACP = FrontmanAiFrontmanClient.FrontmanClient__ACP
module Types = FrontmanAiFrontmanProtocol.FrontmanProtocol__ACP
module ContentBlock = FrontmanAiFrontmanProtocol.FrontmanProtocol__ContentBlock
module Relay = FrontmanAiFrontmanClient.FrontmanClient__Relay
module MCPServer = FrontmanAiFrontmanClient.FrontmanClient__MCP__Server
module Reducer = Client__ConnectionReducer
module RuntimeConfig = Client__RuntimeConfig
module Message = Client__State__Types.Message

let makeToolResult = (~rawOutput, ~content): Message.toolResult => {rawOutput, content}

let toolCallState = (
  ~status: option<Types.toolCallStatus>,
  ~rawInput: option<JSON.t>,
): Message.toolCallState =>
  switch status {
  | Some(Completed) => Message.OutputAvailable
  | Some(Failed) => Message.OutputError
  | Some(Pending | InProgress) | None =>
    rawInput->Option.mapOr(Message.InputStreaming, _ => Message.InputAvailable)
  }

let makeToolCall = (
  ~id,
  ~title,
  ~status,
  ~content,
  ~rawInput,
  ~rawOutput,
  ~parentAgentId,
  ~spawningToolName,
): Message.toolCall => {
  let result = switch (rawOutput, content) {
  | (None, None) => None
  | _ => Some(makeToolResult(~rawOutput, ~content=content->Option.getOr([])))
  }
  {
    id,
    toolName: title,
    inputBuffer: "",
    input: rawInput,
    result,
    errorText: status == Some(Failed) ? Some("Unknown error") : None,
    state: toolCallState(~status, ~rawInput),
    parentAgentId,
    spawningToolName,
  }
}

let getContentBlockText = (block: ContentBlock.t): option<string> =>
  switch block {
  | TextContent({text}) => Some(text)
  | ImageContent(_) | AudioContent(_) | ResourceLink(_) | EmbeddedResource(_) => None
  }

@schema
type frontmanErrorMeta = {
  @as("frontman.dev/agentErrorId")
  agentErrorId: string,
}

let agentErrorId = meta => {
  let json = switch meta {
  | Some(json) => json
  | None => failwith("Frontman error update missing _meta.frontman.dev/agentErrorId")
  }
  S.parseOrThrow(json, ~to=frontmanErrorMetaSchema).agentErrorId
}

let textDeltaBuffer = Client__TextDeltaBuffer.make(
  ~onFlush=(~taskId, ~messageId, ~text, ~agentId) =>
    Client__State.Actions.textDeltaReceived(~taskId, ~messageId, ~text, ~agentId),
  ~onUserFlush=(~taskId, ~messageId, ~blocks, ~agentId) => {
    let (content, annotations) = Client__ACP__MessageCodec.parseUserMessageBlocks(blocks)
    Client__State.Actions.userMessageReceived(
      ~taskId,
      ~id=messageId,
      ~content,
      ~annotations,
      ~agentId,
    )
  },
)
let () = Client__TextDeltaBuffer.active := Some(textDeltaBuffer)

type connectionState = Reducer.Selectors.connectionStatus

@@live
type contextValue = {
  connectionState: connectionState,
  session: option<ACP.session>,
  relay: option<Relay.t>,
  authRedirectUrl: option<string>,
  beginAuthenticationRetry: unit => unit,
  beginLogout: unit => unit,
  createSession: (~onComplete: result<string, string> => unit) => unit,
  clearSession: unit => unit,
  sendPrompt: (
    string,
    ~additionalBlocks: array<ContentBlock.t>,
    ~onComplete: result<Types.promptResult, string> => unit,
    ~_meta: option<JSON.t>,
  ) => unit,
  cancelPrompt: unit => unit,
  retryTurn: string => unit,
  editMessage: (
    ~messageId: string,
    ~text: string,
    ~_meta: option<JSON.t>,
    ~onComplete: result<unit, string> => unit,
  ) => unit,
  loadTask: (string, ~needsHistory: bool, ~onComplete: result<unit, string> => unit) => unit,
  deleteSession: (string, ~onComplete: result<unit, string> => unit) => unit,
}

let defaultContextValue: contextValue = {
  connectionState: Disconnected,
  session: None,
  relay: None,
  authRedirectUrl: None,
  beginAuthenticationRetry: () => (),
  beginLogout: () => (),
  createSession: (~onComplete as _) => (),
  clearSession: () => (),
  sendPrompt: (_, ~additionalBlocks as _, ~onComplete as _, ~_meta as _) => (),
  cancelPrompt: () => (),
  retryTurn: _ => (),
  editMessage: (~messageId as _, ~text as _, ~_meta as _, ~onComplete as _) => (),
  loadTask: (_, ~needsHistory as _, ~onComplete as _) => (),
  deleteSession: (_, ~onComplete as _) => (),
}

let context = React.createContext(defaultContextValue)

module ContextProvider = {
  let make = React.Context.provider(context)
}

let useFrontman = () => React.useContext(context)

module Provider = {
  @react.component
  let make = (
    ~endpoint: string,
    ~tokenUrl: string,
    ~loginUrl: string,
    ~clientName: string="frontman-client",
    ~clientVersion: string="1.0.0",
    ~children: React.element,
  ) => {
    let logACPMessage = React.useCallback0((direction: ACP.messageDirection, payload: JSON.t) => {
      let arrow = direction == Send ? `→` : `←`
      Log.debug(~ctx={"payload": payload}, `ACP ${arrow}`)
    })

    let logMCPMessage = React.useCallback0((direction, payload) => {
      let arrow = direction == FrontmanAiFrontmanClient.FrontmanClient__MCP.Send ? `→` : `←`
      Log.debug(~ctx={"payload": payload}, `MCP ${arrow}`)
    })

    let (state, dispatch) = StateReducer.useReducer(module(Reducer), Reducer.initialState)
    let connectionStateRef = React.useRef(state)

    React.useEffect(() => {
      connectionStateRef.current = state
      None
    }, [state])

    React.useEffect0(() => {
      let baseUrl = Client__RelayBaseUrl.current()

      let runtimeConfig = RuntimeConfig.read()
      let _meta = RuntimeConfig.toMeta(runtimeConfig)
      let relayHeaders = Dict.make()
      runtimeConfig.wpNonce->Option.forEach(nonce => relayHeaders->Dict.set("X-WP-Nonce", nonce))

      let relay = Relay.make(~baseUrl, ~requestHeaders=relayHeaders)
      let toolRegistry = Client__ToolRegistry.forFramework(runtimeConfig.framework)
      let mcpServer = MCPServer.make(~relay, ~serverName=clientName, ~serverVersion=clientVersion)
      let mcpServer = Client__ToolRegistry.registerAll(toolRegistry, mcpServer)

      MCPServer.setImageRefResolver(mcpServer, (uri, ~taskId) => {
        let state = StateStore.getState(Client__State__Store.store)
        Client__State.Selectors.resolveImageRef(state, ~taskId, ~uri)->Option.map(
          ({base64, mediaType}) => {MCPServer.base64, mediaType},
        )
      })

      let config: Reducer.initConfig = {
        endpoint,
        tokenUrl,
        loginUrl,
        clientName,
        clientVersion,
        onACPMessage: logACPMessage,
        _meta,
        onTitleUpdated: Some(
          (taskId, title) => {
            Client__State.Actions.updateTaskTitle(~taskId, ~title)
          },
        ),
      }

      dispatch(Initialize({config, relay, mcpServer}))

      Some(
        () => {
          textDeltaBuffer.reset()
          let state = connectionStateRef.current
          state.abortController->Option.forEach(controller =>
            WebAPI.AbortController.abort(controller)
          )
          state.relayInstance->Option.forEach(relay => Relay.disconnect(relay))
          let activeSession = switch state.session {
          | SessionActive(session) => Some(session)
          | NoSession | SessionCreating(_) | SessionError(_) => None
          }
          switch state.acp {
          | ACPConnected(conn) => ACP.disconnect(conn, ~session=?activeSession)
          | ACPDisconnected | ACPConnecting | ACPLoggingOut | ACPAuthRequired(_) | ACPError(_) => ()
          }
          dispatch(Dispose)
        },
      )
    })

    let handleTitleUpdated = React.useCallback0((taskId: string, title: string) => {
      Client__State.Actions.updateTaskTitle(~taskId, ~title)
    })

    let handleSessionUpdate = React.useCallback0((
      sessionId: string,
      update: Types.sessionUpdate,
    ) => {
      let taskId = sessionId
      switch update {
      | AgentMessageChunk({messageId, content, _meta: {agentId}}) =>
        getContentBlockText(content)->Option.forEach(text => {
          textDeltaBuffer.add(~taskId, ~messageId, ~text, ~agentId)
        })
      | UserMessageChunk({messageId, content, _meta}) =>
        textDeltaBuffer.addUserBlock(~taskId, ~messageId, ~block=content, ~agentId=_meta.agentId)
      | GenericAgentMessageChunk(_) | GenericUserMessageChunk(_) =>
        failwith("Frontman UI requires negotiated agent attribution")
      | Unknown(_) => ()
      | ToolCall({
          toolCallId,
          title,
          status,
          content,
          rawInput,
          rawOutput,
          parentAgentId,
          spawningToolName,
          _,
        }) =>
        Client__TextDeltaBuffer.flush()
        Client__State.Actions.toolCallReceived(
          ~taskId,
          ~toolCall=makeToolCall(
            ~id=toolCallId,
            ~title,
            ~status,
            ~content,
            ~rawInput,
            ~rawOutput,
            ~parentAgentId,
            ~spawningToolName,
          ),
        )
      | ToolCallUpdate({toolCallId, status, content, rawInput, rawOutput}) =>
        Client__TextDeltaBuffer.flush()
        let text = () =>
          content->Option.flatMap(c =>
            c->Array.findMap(
              item =>
                switch item {
                | Content({content: TextContent({text})}) => Some(text)
                | _ => None
                },
            )
          )
        rawInput->Option.forEach(input => {
          Client__State.Actions.toolInputReceived(~taskId, ~id=toolCallId, ~input)
        })
        switch (rawOutput, content, status) {
        | (None, None, Some(Completed)) =>
          Client__State.Actions.toolResultReceived(
            ~taskId,
            ~id=toolCallId,
            ~rawOutput,
            ~content,
            ~complete=true,
          )
        | (None, None, _) => ()
        | _ =>
          Client__State.Actions.toolResultReceived(
            ~taskId,
            ~id=toolCallId,
            ~rawOutput,
            ~content,
            ~complete=status == Some(Completed),
          )
        }
        switch status {
        | Some(Pending) => ()
        | Some(Completed) => ()
        | Some(Failed) =>
          Client__State.Actions.toolErrorReceived(
            ~taskId,
            ~id=toolCallId,
            ~error=text()->Option.getOr("Unknown error"),
          )
        | Some(InProgress) => ()
        | None => ()
        }
      | Plan({entries}) =>
        Client__TextDeltaBuffer.flush()
        Client__State.Actions.planReceived(~taskId, ~entries)
      | StateUpdate({state, stopReason: _}) =>
        Client__TextDeltaBuffer.flush()
        switch state {
        | Running => Client__State.Actions.executionStateRunning(~taskId)
        | Idle => Client__State.Actions.executionStateIdle(~taskId)
        | RequiresAction => Client__State.Actions.executionStateRequiresAction(~taskId)
        }
      | ConfigOptionUpdate({configOptions}) =>
        Client__TextDeltaBuffer.flush()
        Client__State.Actions.configOptionsReceived(~configOptions)
      | CurrentModeUpdate(_) => Client__TextDeltaBuffer.flush()
      | Error({_meta, message, retryAt, attempt, maxAttempts, category}) =>
        Client__TextDeltaBuffer.flush()
        switch retryAt {
        | Some(retryAtStr) =>
          let retryAtMs = Date.fromString(retryAtStr)->Date.getTime
          let retryStatus: Client__Task__Types.Task.retryStatus = {
            attempt: attempt->Option.getOr(1),
            maxAttempts: maxAttempts->Option.getOr(5),
            retryAt: retryAtMs,
            error: message,
          }
          Client__State.Actions.retryingStatusReceived(~taskId, ~retryStatus)
        | None =>
          Client__State.Actions.agentErrorReceived(
            ~taskId,
            ~id=agentErrorId(_meta),
            ~error=message,
            ~category=Client__ErrorCategory.fromAcpCategory(category),
          )
        }
      }
    })

    let createSession = React.useCallback1((~onComplete: result<string, string> => unit) => {
      dispatch(
        CreateSession({
          sessionId: WebAPI.Window.current->WebAPI.Window.crypto->WebAPI.Crypto.randomUUID,
          onUpdate: handleSessionUpdate,
          onTitleUpdated: handleTitleUpdated,
          onMcpMessage: logMCPMessage,
          onComplete,
        }),
      )
    }, [dispatch])

    let clearSession = React.useCallback1(() => dispatch(ClearSession), [dispatch])

    let sendPrompt = React.useCallback1((text: string, ~additionalBlocks, ~onComplete, ~_meta) => {
      dispatch(SendPrompt({text, additionalBlocks, onComplete, _meta}))
    }, [dispatch])

    let cancelPrompt = React.useCallback1(() => {
      dispatch(CancelPrompt)
    }, [dispatch])

    let retryTurn = React.useCallback1((retriedErrorId: string) => {
      dispatch(RetryTurn({retriedErrorId: retriedErrorId}))
    }, [dispatch])

    let editMessage = React.useCallback1((~messageId, ~text, ~_meta, ~onComplete) => {
      dispatch(EditMessage({messageId, text, _meta, onComplete}))
    }, [dispatch])

    let loadTask = React.useCallback1((taskId: string, ~needsHistory, ~onComplete) => {
      dispatch(
        LoadTask({
          taskId,
          needsHistory,
          onUpdate: handleSessionUpdate,
          onTitleUpdated: handleTitleUpdated,
          onMcpMessage: logMCPMessage,
          onComplete,
        }),
      )
    }, [dispatch])

    let deleteSession = React.useCallback1((taskId: string, ~onComplete) => {
      dispatch(DeleteSession({taskId, onComplete}))
    }, [dispatch])

    let authRedirectUrl = Reducer.Selectors.getAuthRedirectUrl(state)
    let beginAuthenticationRetry = React.useCallback1(() => {
      dispatch(BeginAuthenticationRetry)
    }, [dispatch])
    let beginLogout = React.useCallback1(() => dispatch(BeginLogout), [dispatch])

    let contextValue: contextValue = {
      connectionState: Reducer.Selectors.getConnectionStatus(state),
      session: Reducer.Selectors.getSession(state),
      relay: state.relayInstance,
      authRedirectUrl,
      beginAuthenticationRetry,
      beginLogout,
      createSession,
      clearSession,
      sendPrompt,
      cancelPrompt,
      retryTurn,
      editMessage,
      loadTask,
      deleteSession,
    }

    <ContextProvider value={contextValue}> {children} </ContextProvider>
  }
}
