# Frontman Server
# Copyright (C) 2025 Frontman AI
#
# Licensed under the AGPL-3.0 — see LICENSE for details.
# Additional terms apply — see AI-SUPPLEMENTARY-TERMS.md

defmodule FrontmanServerWeb.TaskChannel do
  @moduledoc """
  Channel for task-specific ACP events.

  Clients join this channel after creating a task via the
  tasks channel. Handles prompt messages and streams
  agent responses back to the client.
  """
  use FrontmanServerWeb, :channel
  require Logger

  alias AgentClientProtocol, as: ACP
  alias AgentClientProtocol.History, as: ACPHistory
  alias FrontmanServer.Agents
  alias FrontmanServer.Frameworks
  alias FrontmanServer.Observability.SentryContext
  alias FrontmanServer.Providers
  alias FrontmanServer.Tasks
  alias FrontmanServer.Tasks.History, as: TaskHistory
  alias FrontmanServer.Tasks.RetryCoordinator
  alias FrontmanServer.Tasks.Todos.Todo
  alias FrontmanServer.Tools
  alias FrontmanServerWeb.TaskChannel.MCPInitializer
  alias ModelContextProtocol, as: MCP

  @acp_message ACP.event_acp_message()
  @acp_title_updated ACP.event_title_updated()
  @acp_method_session_prompt ACP.method_session_prompt()
  @acp_method_session_cancel ACP.method_session_cancel()
  @acp_method_session_load ACP.method_session_load()
  @impl true
  def join("task:" <> task_id, _params, socket) do
    scope = socket.assigns.scope

    case Tasks.get_task(scope, task_id) do
      {:ok, task} ->
        {:ok, history} = TaskHistory.new(task.interaction_rows)
        active_turn = TaskHistory.active_turn_context(history)

        SentryContext.set_task_scope_context(scope, task_id)

        Logger.info("Client joining: #{task_id}, socket_id: #{inspect(self())}")

        {init_state, init_actions} = MCPInitializer.start(task_id, scope, task.framework)

        socket =
          socket
          |> assign(:task_id, task_id)
          |> assign(:framework, task.framework)
          |> assign(:mcp_init_state, init_state)
          |> assign(:mcp_tools, [])
          |> assign(:mcp_status, :pending)
          |> assign(:session_loaded, false)
          |> assign(:active_turn, active_turn)
          |> assign(:pending_mcp_tool_requests, %{})

        send(self(), {:start_mcp_init, init_actions})

        {:ok, %{task_id: task_id}, socket}

      {:error, :not_found} ->
        Logger.info("Client tried to join non-existent task: #{task_id}")
        {:error, %{reason: "task_not_found"}}
    end
  end

  @impl true
  def handle_in(@acp_message, payload, socket) do
    parsed = JsonRpc.parse(payload)

    Logger.info("Received ACP message")

    case parsed do
      {:ok, {:request, id, @acp_method_session_prompt, params}} ->
        handle_prompt(id, params, socket)

      {:ok, {:notification, @acp_method_session_cancel, params}} ->
        handle_cancel(params, socket)

      {:ok, {:request, id, @acp_method_session_load, params}} ->
        handle_session_load(id, params, socket)

      {:ok, {:request, id, "session/edit_message", params}} ->
        handle_edit_message(id, params, socket)

      {:ok, {:request, id, method, _params}} ->
        reply_acp_error(
          socket,
          id,
          JsonRpc.error_method_not_found(),
          "Method not found: #{method}"
        )

      {:ok, {:notification, "session/retry_turn", %{"retriedErrorId" => retried_error_id}}} ->
        handle_retry_turn(retried_error_id, socket)

      {:ok, {:notification, _method, _params}} ->
        {:noreply, socket}

      {:error, reason} ->
        handle_invalid_acp_message(reason, payload, socket)
    end
  end

  @impl true
  def handle_in("mcp:message", payload, socket) do
    case JsonRpc.parse_response(payload) do
      {:ok, {:success, id, result}} ->
        handle_mcp_response(id, result, socket)

      {:ok, {:error, id, error}} ->
        handle_mcp_error(id, error, socket)

      {:error, reason} ->
        Logger.error("Invalid MCP response")

        error_notification =
          JsonRpc.notification("error", %{
            "message" => "Invalid JSON-RPC response",
            "reason" => Atom.to_string(reason)
          })

        push(socket, "mcp:message", error_notification)

        {:noreply, socket}
    end
  end

  @impl true
  def handle_info({:start_mcp_init, actions}, socket) do
    socket = execute_init_actions(actions, socket)
    {:noreply, socket}
  end

  def handle_info({:run_next_turn, execution}, socket) do
    case Tasks.run_next_turn(socket.assigns.scope, socket.assigns.task_id, execution) do
      result when result in [:ok, :already_running, :no_accepted_messages] ->
        :ok

      {:error, reason} ->
        Logger.error("Failed to run next turn: #{inspect(reason)}")
    end

    {:noreply, socket}
  end

  def handle_info({:execution_chunk, turn_number, metadata, chunk}, socket) do
    {:noreply, handle_execution_chunk(socket, turn_number, metadata, chunk)}
  end

  def handle_info(
        {:interaction,
         %{
           id: turn_started_id,
           data: %Tasks.Interaction.TurnStarted{} = interaction,
           turn_number: turn_number
         }},
        socket
      ) do
    handle_turn_started(interaction, turn_started_id, turn_number, socket)
  end

  def handle_info({:interaction, %{data: interaction, turn_number: turn_number}}, socket) do
    handle_interaction(interaction, turn_number, socket)
  end

  def handle_info({:fire_retry, token}, socket) do
    case socket.assigns[:retry_state] do
      %{timer_token: ^token, retried_error_id: retried_error_id} ->
        retry_turn(socket, retried_error_id)

      _stale_or_nil ->
        :ok
    end

    {:noreply, socket}
  end

  def handle_info({:task_title_changed, task_id, title}, socket) do
    push(socket, @acp_title_updated, %{"sessionId" => task_id, "title" => title})
    {:noreply, socket}
  end

  def handle_info(msg, _socket) do
    raise "Unhandled message in TaskChannel: #{inspect(msg)}"
  end

  defp handle_turn_started(turn, turn_started_id, turn_number, socket) do
    task_id = socket.assigns.task_id
    notification = ACP.build_state_update_notification(task_id, "running")
    push(socket, @acp_message, notification)

    context = %{
      agent_id: turn.agent_id,
      turn_number: turn_number,
      turn_started_id: turn_started_id
    }

    {:noreply, assign(socket, :active_turn, context)}
  end

  defp handle_interaction(%Tasks.Interaction.ToolCall{} = tool_call, _turn_number, socket) do
    task_id = socket.assigns.task_id

    announced = socket.assigns[:announced_tool_calls] || MapSet.new()

    notification =
      case MapSet.member?(announced, tool_call.tool_call_id) do
        false ->
          ACP.tool_call_create(
            task_id,
            tool_call.tool_call_id,
            tool_call.tool_name,
            "other",
            DateTime.utc_now(),
            ACP.tool_call_status_pending(),
            tool_call.arguments
          )

        true ->
          ACP.tool_call_update(
            task_id,
            tool_call.tool_call_id,
            ACP.tool_call_status_pending(),
            nil,
            tool_call.arguments
          )
      end

    push(socket, @acp_message, notification)

    case Tools.execution_target(tool_call.tool_name) do
      :backend ->
        {:noreply, socket}

      :mcp ->
        route_to_mcp(tool_call, socket)
    end
  end

  defp handle_interaction(%Tasks.Interaction.ToolResult{} = tool_result, _turn_number, socket) do
    task_id = socket.assigns.task_id

    notification =
      ACP.tool_call_update(
        task_id,
        tool_result.tool_call_id,
        ACP.tool_call_status(tool_result.is_error),
        ACP.Content.from_tool_result(tool_result.result),
        nil,
        tool_result.result["structuredContent"]
      )

    push(socket, @acp_message, notification)

    case {Tools.todo_mutation?(tool_result.tool_name), tool_result.is_error} do
      {true, false} -> push_current_todo_plan(socket)
      {true, true} -> :ok
      {false, _is_error} -> :ok
    end

    {:noreply, socket}
  end

  defp handle_interaction(%Tasks.Interaction.AgentCompleted{}, turn_number, socket) do
    finalize_turn(socket, {:completed, ACP.stop_reason_end_turn()}, turn_number)
  end

  defp handle_interaction(%Tasks.Interaction.AgentRetry{}, turn_number, socket) do
    context =
      case socket.assigns.active_turn do
        %{turn_number: ^turn_number} = context -> context
        _missing -> load_turn_context!(socket, turn_number)
      end

    {:noreply, assign(socket, :active_turn, context)}
  end

  defp handle_interaction(%Tasks.Interaction.AgentPaused{}, turn_number, socket) do
    finalize_turn(socket, :requires_action, turn_number)
  end

  defp handle_interaction(%Tasks.Interaction.AgentError{kind: "cancelled"}, turn_number, socket) do
    finalize_turn(socket, {:completed, ACP.stop_reason_cancelled()}, turn_number)
  end

  defp handle_interaction(
         %Tasks.Interaction.AgentError{retryable: true} = error,
         turn_number,
         socket
       ) do
    handle_transient_error(
      socket,
      %{
        message: error.error,
        category: error.category,
        retryable: true,
        retried_error_id: error.id
      },
      turn_number
    )
  end

  defp handle_interaction(%Tasks.Interaction.AgentError{} = error, turn_number, socket) do
    finalize_turn(socket, {:error, error.id, error.error, error.category}, turn_number)
  end

  defp handle_interaction(_interaction, _turn_number, socket) do
    {:noreply, socket}
  end

  defp load_turn_context!(socket, turn_number) do
    {:ok, task} = Tasks.get_task(socket.assigns.scope, socket.assigns.task_id)
    {:ok, history} = TaskHistory.new(task.interaction_rows)
    TaskHistory.turn_context(history, turn_number)
  end

  defp handle_mcp_response(id, result, socket) do
    init_state = socket.assigns[:mcp_init_state]

    if mcp_initialization_request?(init_state, id) do
      {new_state, actions} = MCPInitializer.handle_response(init_state, id, result)
      socket = assign(socket, :mcp_init_state, new_state)
      {:noreply, execute_init_actions(actions, socket)}
    else
      handle_tool_call_response_by_id(id, result, socket)
    end
  end

  defp handle_tool_call_response_by_id(id, result, socket) when is_integer(id) do
    case pop_mcp_tool_request(socket, id) do
      {:ok, tool_call_id, socket} ->
        case open_tool_call(socket, tool_call_id) do
          {:ok, tool_call} ->
            handle_tool_call_response(tool_call, result, socket)

          :error ->
            Logger.warning(
              "Received MCP response for unknown tool_call_id: #{inspect(tool_call_id)}"
            )

            {:noreply, socket}
        end

      :error ->
        unknown_mcp_response(id, socket)
    end
  end

  defp handle_tool_call_response_by_id(id, _result, socket), do: unknown_mcp_response(id, socket)

  defp pop_mcp_tool_request(socket, request_id) do
    case Map.pop(socket.assigns.pending_mcp_tool_requests, request_id) do
      {nil, _pending} ->
        :error

      {tool_call_id, pending} ->
        {:ok, tool_call_id, assign(socket, :pending_mcp_tool_requests, pending)}
    end
  end

  defp open_tool_call(socket, tool_call_id) do
    with {:ok, _turn_number, tool_calls} when is_list(tool_calls) <-
           Tasks.get_active_run_unresolved_tool_calls(
             socket.assigns.scope,
             socket.assigns.task_id
           ),
         %Tasks.Interaction.ToolCall{} = tool_call <-
           Enum.find(tool_calls, &(&1.tool_call_id == tool_call_id)) do
      {:ok, tool_call}
    else
      _ -> :error
    end
  end

  defp unknown_mcp_response(_id, socket) do
    Logger.warning("Received MCP response for unknown request")
    {:noreply, socket}
  end

  defp handle_tool_call_response(tool_call, result, socket) do
    {:noreply, persist_tool_call_result(tool_call, result, socket)}
  end

  defp persist_tool_call_result(tool_call, result, socket) do
    task_id = socket.assigns.task_id
    scope = socket.assigns.scope

    case Tasks.resolve_tool_request(
           scope,
           task_id,
           %{id: tool_call.tool_call_id, name: tool_call.tool_name},
           result
         ) do
      {:ok, interaction, executor_status} ->
        status = ACP.tool_call_status(interaction.is_error)

        notification =
          ACP.tool_call_update(
            task_id,
            interaction.tool_call_id,
            status,
            ACP.Content.from_tool_result(interaction.result),
            nil,
            interaction.result["structuredContent"]
          )

        push(socket, @acp_message, notification)
        Logger.info("Tool #{interaction.tool_name} #{status}")

        resume_after_tool_result(executor_status, socket, scope, task_id)

      {:error, _reason} ->
        Logger.warning("Failed to store tool result")

        socket
    end
  end

  defp resume_after_tool_result(:notified, socket, _scope, _task_id), do: socket

  defp resume_after_tool_result(:no_executor, socket, scope, task_id) do
    case Tasks.get_active_run_unresolved_tool_calls(scope, task_id) do
      {:ok, _turn_number, []} ->
        Logger.info(
          "Active agent run has no unresolved tool calls for #{task_id}, resuming agent"
        )

        resume_agent(socket, scope, task_id)

      {:ok, _turn_number, [_ | _]} ->
        socket

      {:ok, :no_active_run} ->
        socket
    end
  end

  defp resume_agent(socket, scope, task_id) do
    Tasks.resume_execution(scope, task_id, %{
      mcp_tools: socket.assigns.mcp_tools,
      project_traits: Frameworks.project_traits_from_meta(nil, socket.assigns.framework)
    })

    socket
  end

  defp handle_mcp_error(id, error, socket) do
    init_state = socket.assigns[:mcp_init_state]

    if mcp_initialization_request?(init_state, id) do
      {new_state, actions} = MCPInitializer.handle_error(init_state, id, error)
      socket = assign(socket, :mcp_init_state, new_state)
      {:noreply, execute_init_actions(actions, socket)}
    else
      handle_tool_call_error_by_id(id, error, socket)
    end
  end

  defp mcp_initialization_request?(%{} = init_state, id) when is_integer(id) do
    id in [
      init_state.mcp_init_request_id,
      init_state.tools_request_id,
      init_state.project_rules_request_id,
      init_state.project_structure_request_id
    ]
  end

  defp mcp_initialization_request?(_init_state, _id), do: false

  defp handle_tool_call_error_by_id(id, error, socket) when is_integer(id) do
    case pop_mcp_tool_request(socket, id) do
      {:ok, tool_call_id, socket} ->
        case open_tool_call(socket, tool_call_id) do
          {:ok, tool_call} ->
            handle_tool_call_error(tool_call, error, socket)

          :error ->
            Logger.warning(
              "Received MCP error for unknown tool_call_id: #{inspect(tool_call_id)}"
            )

            {:noreply, socket}
        end

      :error ->
        unknown_mcp_error(id, socket)
    end
  end

  defp handle_tool_call_error_by_id(id, _error, socket), do: unknown_mcp_error(id, socket)

  defp unknown_mcp_error(_id, socket) do
    Logger.warning("Received MCP error for unknown request")
    {:noreply, socket}
  end

  defp handle_tool_call_error(tool_call, error, socket) do
    task_id = socket.assigns.task_id
    error_message = error["message"] || "Unknown MCP error"

    metadata = [
      error_type: "mcp_tool_error",
      tool_name: tool_call.tool_name,
      tool_call_id: tool_call.tool_call_id,
      task_id: task_id,
      error_code: mcp_error_code(error)
    ]

    Logger.error("MCP tool execution failed", metadata)

    result = ModelContextProtocol.tool_result_error(error_message)
    {:noreply, persist_tool_call_result(tool_call, result, socket)}
  end

  defp handle_prompt(id, params, socket) do
    if socket.assigns[:mcp_status] == :failed do
      Logger.warning(
        "Processing prompt with failed MCP initialization for task #{socket.assigns.task_id}"
      )
    end

    process_prompt(id, params, socket)
  end

  defp mcp_error_code(%{"code" => code}) when is_integer(code), do: code
  defp mcp_error_code(_error), do: :unknown

  defp handle_cancel(_params, socket) do
    task_id = socket.assigns.task_id
    Logger.info("Cancel notification received for task #{task_id}")

    had_retry = socket.assigns[:retry_state] != nil
    socket = assign(socket, :retry_state, RetryCoordinator.clear(socket.assigns[:retry_state]))

    case Tasks.cancel_execution(socket.assigns.scope, task_id) do
      :ok ->
        Logger.info("Agent cancel signal sent for task #{task_id}")
        {:noreply, socket}

      {:error, :not_running} ->
        Logger.info("Cancel notification for task #{task_id}: no agent running")

        if had_retry do
          finalize_turn(socket, {:completed, ACP.stop_reason_cancelled()}, nil)
        else
          {:noreply, socket}
        end

      {:error, :not_found} ->
        {:noreply, socket}
    end
  end

  defp handle_session_load(id, %{"sessionId" => task_id}, socket)
       when task_id == socket.assigns.task_id do
    scope = socket.assigns.scope
    Logger.info("ACP session/load request received on session channel for: #{task_id}")

    case Tasks.get_task(scope, task_id) do
      {:ok, task} ->
        {:ok, history} = TaskHistory.new(task.interaction_rows)
        {:ok, replay} = ACPHistory.build(history, task.id, Agents.list_agents(scope))
        Enum.each(replay.notifications, &push(socket, @acp_message, &1))

        push(
          socket,
          @acp_message,
          JsonRpc.success_response(
            id,
            ACP.build_session_load_result(
              scope
              |> Providers.model_config_data()
              |> ACP.build_model_config_options()
            )
          )
        )

        push_current_todo_plan(socket, Tasks.list_todos(task))

        socket =
          socket
          |> assign(:session_loaded, true)
          |> assign(:active_turn, TaskHistory.active_turn_context(history))
          |> redispatch_unresolved_tool_calls()

        wake_runner(socket, nil)

        {:noreply, socket}

      {:error, :not_found} ->
        push_acp_error(socket, id, JsonRpc.error_invalid_params(), "Session not found")
    end
  end

  defp handle_session_load(id, _params, socket) do
    push_acp_error(socket, id, JsonRpc.error_invalid_params(), "Session does not match channel")
  end

  defp handle_edit_message(
         id,
         %{"sessionId" => task_id, "messageId" => message_id, "text" => text, "_meta" => meta},
         socket
       )
       when task_id == socket.assigns.task_id and is_map(meta) do
    scope = socket.assigns.scope

    case Providers.model_from_client_params(meta["model"]) do
      {:ok, model} ->
        with {:ok, agent_id} <-
               Agents.resolve_agent_id(scope, meta["agent"] || Agents.default_agent_id(scope)),
             :ok <- Tasks.edit_message(scope, task_id, message_id, text, model, agent_id) do
          Logger.info("Message edit accepted for task #{task_id}")
          reply_acp_ok(socket, id)
        else
          {:error, :run_active} ->
            reply_invalid_params(socket, id, "Cannot edit a message while the agent is running")

          {:error, :empty_message} ->
            reply_invalid_params(socket, id, "Edited message cannot be empty")

          {:error, :not_found} ->
            reply_invalid_params(socket, id, "Message not found")

          {:error, reason} when reason in [:missing_agent, :unknown_agent] ->
            reply_invalid_params(socket, id, "Unknown agent")

          {:error, reason} ->
            Logger.error("Failed to edit message: #{inspect(reason)}")
            reply_acp_error(socket, id, -32_000, inspect(reason))
        end

      :error ->
        reply_invalid_params(socket, id, "Model is required")
    end
  end

  defp handle_edit_message(id, _params, socket),
    do: reply_invalid_params(socket, id, "Invalid session/edit_message params")

  defp reply_acp_ok(socket, id),
    do: {:reply, {:ok, %{@acp_message => JsonRpc.success_response(id, %{})}}, socket}

  defp process_prompt(id, %{"prompt" => content_blocks, "_meta" => meta}, socket)
       when is_map(meta) do
    task_id = socket.assigns.task_id
    scope = socket.assigns.scope

    case Providers.model_from_client_params(meta["model"]) do
      {:ok, model} ->
        Logger.info("process_prompt", %{task_id: task_id, model: model})

        with {:ok, agent_id} <-
               Agents.resolve_agent_id(scope, meta["agent"] || Agents.default_agent_id(scope)),
             {:ok, row} <-
               Tasks.submit_user_message(
                 scope,
                 %{
                   task_id: task_id,
                   message_id: meta["frontman.dev/messageId"],
                   message: content_blocks,
                   model: model,
                   agent_id: agent_id
                 }
               ) do
          push_user_message_chunks(socket, task_id, row)

          wake_runner(socket, meta)

          Logger.info("User message accepted for task #{task_id}")
          {:reply, {:ok, %{@acp_message => JsonRpc.success_response(id, %{})}}, socket}
        else
          {:error, %Ecto.Changeset{} = changeset} ->
            {message, _metadata} = Keyword.fetch!(changeset.errors, :id)
            reply_invalid_params(socket, id, "Message ID #{message}")

          {:error, :missing_agent} ->
            reply_invalid_params(socket, id, "Agent is required")

          {:error, :unknown_agent} ->
            reply_invalid_params(socket, id, "Unknown agent")

          {:error, {:invalid_content_block, message}} ->
            Logger.error("Failed to add user message: #{message}")
            reply_invalid_params(socket, id, message)

          {:error, reason} ->
            Logger.error("Failed to add user message: #{inspect(reason)}")
            reply_acp_error(socket, id, -32_000, inspect(reason))
        end

      :error ->
        reply_invalid_params(socket, id, "Model is required")
    end
  end

  defp push_user_message_chunks(socket, task_id, row) do
    %{row: row, agent_id: row.data.agent_id}
    |> ACPHistory.encode_row(task_id)
    |> Enum.each(&push(socket, @acp_message, &1))
  end

  defp reply_acp_error(socket, id, code, message) do
    {:reply, {:ok, %{@acp_message => JsonRpc.error_response(id, code, message)}}, socket}
  end

  defp reply_invalid_params(socket, id, message),
    do: reply_acp_error(socket, id, JsonRpc.error_invalid_params(), message)

  defp push_acp_error(socket, id, code, message) do
    push(socket, @acp_message, JsonRpc.error_response(id, code, message))
    {:noreply, socket}
  end

  defp handle_execution_chunk(
         socket,
         turn_number,
         %{
           turn_started_id: turn_started_id,
           agent_id: agent_id,
           ordinal: ordinal,
           timestamp: timestamp
         },
         %{type: :content, text: text}
       )
       when is_binary(text) and text != "" do
    validate_execution_context!(socket, turn_number, turn_started_id, agent_id)
    task_id = socket.assigns.task_id
    message_id = ACP.agent_message_id(turn_started_id, ordinal)

    notification =
      ACP.build_agent_message_chunk_notification(task_id, text, timestamp, message_id, agent_id)

    push(socket, @acp_message, notification)
    socket
  end

  defp handle_execution_chunk(
         socket,
         turn_number,
         %{turn_started_id: turn_started_id, agent_id: agent_id},
         %{type: :tool_call, name: name, metadata: %{id: id}}
       )
       when is_binary(name) and is_binary(id) do
    validate_execution_context!(socket, turn_number, turn_started_id, agent_id)
    announce_stream_tool_call_once(socket, id, name)
  end

  defp handle_execution_chunk(socket, _turn_number, _metadata, %{type: :content, text: ""}),
    do: socket

  defp handle_execution_chunk(_socket, _turn_number, metadata, %{type: type})
       when type in [:content, :tool_call],
       do: raise("Invalid #{type} chunk metadata: #{inspect(metadata)}")

  defp handle_execution_chunk(socket, _turn_number, _metadata, _chunk), do: socket

  defp validate_execution_context!(
         %{assigns: %{active_turn: active_turn}},
         turn_number,
         turn_started_id,
         agent_id
       ) do
    expected = %{
      agent_id: agent_id,
      turn_number: turn_number,
      turn_started_id: turn_started_id
    }

    case active_turn do
      ^expected ->
        :ok

      _other ->
        raise "Stale execution chunk: expected #{inspect(active_turn)}, got #{inspect(expected)}"
    end
  end

  defp announce_stream_tool_call_once(socket, id, name) do
    announced = socket.assigns[:announced_tool_calls] || MapSet.new()

    case MapSet.member?(announced, id) do
      true ->
        socket

      false ->
        task_id = socket.assigns.task_id

        notification =
          ACP.tool_call_create(
            task_id,
            id,
            name,
            "other",
            DateTime.utc_now(),
            ACP.tool_call_status_pending()
          )

        push(socket, @acp_message, notification)
        assign(socket, :announced_tool_calls, MapSet.put(announced, id))
    end
  end

  defp handle_invalid_acp_message(_reason, payload, socket) do
    Logger.error("Invalid ACP message in task channel")

    case payload do
      %{"id" => id} ->
        push_acp_error(socket, id, JsonRpc.error_invalid_request(), "Invalid JSON-RPC message")

      _ ->
        {:noreply, socket}
    end
  end

  defp handle_retry_turn(retried_error_id, socket) do
    retry_turn(socket, retried_error_id)
    {:noreply, socket}
  end

  defp retry_turn(socket, retried_error_id) do
    case Tasks.retry_execution(
           socket.assigns.scope,
           socket.assigns.task_id,
           retried_error_id,
           %{
             model: nil,
             mcp_tools: socket.assigns.mcp_tools,
             project_traits: Frameworks.project_traits_from_meta(nil, socket.assigns.framework)
           }
         ) do
      :ok ->
        notification = ACP.build_state_update_notification(socket.assigns.task_id, "running")
        push(socket, @acp_message, notification)

      {:error, reason} ->
        unless reason in [:not_found, :stale_turn] do
          Logger.warning("Retry turn failed: #{inspect(reason)}")
        end

        push_agent_error(
          socket,
          retried_error_id,
          "That response can no longer be retried. Please send a new message instead.",
          "retry_unavailable"
        )
    end
  end

  defp handle_transient_error(socket, error_info, turn_number) do
    case RetryCoordinator.handle_error(socket.assigns[:retry_state], error_info) do
      {:exhausted, error_info} ->
        finalize_turn(
          socket,
          {:error, error_info.retried_error_id, error_info.message, error_info.category},
          turn_number
        )

      {:retry_scheduled, state, notification} ->
        push_agent_error(
          socket,
          state.retried_error_id,
          notification.message,
          notification.category,
          retry_at: notification.retry_at,
          attempt: notification.attempt,
          max_attempts: notification.max_attempts
        )

        {:noreply, assign(socket, :retry_state, state)}
    end
  end

  defp finalize_turn(socket, outcome, turn_number) do
    task_id = socket.assigns.task_id

    case {turn_number, socket.assigns.active_turn} do
      {nil, _active_turn} -> :ok
      {_turn_number, nil} -> :ok
      {turn_number, %{turn_number: turn_number}} -> :ok
      {_turn_number, active_turn} -> raise "Stale turn finalization: #{inspect(active_turn)}"
    end

    socket =
      socket
      |> assign(:retry_state, RetryCoordinator.clear(socket.assigns[:retry_state]))
      |> assign(:active_turn, nil)

    case outcome do
      {:completed, stop_reason} ->
        notification = ACP.build_state_update_notification(task_id, "idle", stop_reason)
        push(socket, @acp_message, notification)
        wake_runner(socket, nil)
        {:noreply, socket}

      :requires_action ->
        notification = ACP.build_state_update_notification(task_id, "requires_action")
        push(socket, @acp_message, notification)
        {:noreply, socket}

      {:error, agent_error_id, message, category} ->
        push_agent_error(socket, agent_error_id, message, category)
        wake_runner(socket, nil)
        {:noreply, socket}
    end
  end

  defp push_agent_error(socket, agent_error_id, message, category, opts \\ []) do
    notification =
      ACP.build_error_notification(
        socket.assigns.task_id,
        message,
        DateTime.utc_now(),
        Keyword.merge(opts, category: category, agent_error_id: agent_error_id)
      )

    push(socket, @acp_message, notification)
  end

  defp wake_runner(socket, meta) do
    case socket.assigns[:mcp_status] do
      status when status in [:ready, :failed] ->
        send(self(), {:run_next_turn, execution_context(socket, meta)})

      _pending ->
        :ok
    end
  end

  defp execution_context(socket, meta) do
    model =
      case Providers.model_from_client_params(meta && meta["model"]) do
        {:ok, model} -> model
        :error -> nil
      end

    %{
      model: model,
      mcp_tools: socket.assigns.mcp_tools,
      project_traits: Frameworks.project_traits_from_meta(meta, socket.assigns.framework)
    }
  end

  defp execute_init_actions(actions, socket) do
    apply_init_actions(actions, socket)
  end

  defp apply_init_actions([], socket), do: socket

  defp apply_init_actions([action | rest], socket) do
    socket = apply_init_action(socket, action)
    apply_init_actions(rest, socket)
  end

  defp apply_init_action(socket, {:push_mcp, msg}) do
    push(socket, "mcp:message", msg)
    socket
  end

  defp apply_init_action(socket, {:push_acp, msg}) do
    push(socket, @acp_message, msg)
    socket
  end

  defp apply_init_action(socket, {:initialization_complete, data}) do
    task_id = socket.assigns.task_id
    Logger.info("MCP initialization complete for task #{task_id}")

    socket
    |> assign(:mcp_status, :ready)
    |> assign(:mcp_capabilities, data.mcp_capabilities)
    |> assign(:mcp_server_info, data.mcp_server_info)
    |> assign(:mcp_tools, data.tools)
    |> redispatch_unresolved_tool_calls()
    |> tap(&wake_runner(&1, nil))
  end

  defp apply_init_action(socket, {:initialization_failed, error}) do
    Logger.error("MCP initialization failed")

    socket
    |> assign(:mcp_status, :failed)
    |> assign(:mcp_error, error)
    |> redispatch_unresolved_tool_calls()
    |> tap(&wake_runner(&1, nil))
  end

  defp redispatch_unresolved_tool_calls(
         %{assigns: %{session_loaded: true, mcp_status: status}} = socket
       )
       when status in [:ready, :failed] do
    case Tasks.get_active_run_unresolved_tool_calls(socket.assigns.scope, socket.assigns.task_id) do
      {:ok, turn_number, tool_calls} when is_list(tool_calls) ->
        Enum.reduce(tool_calls, socket, &redispatch_unresolved_tool_call(&2, &1, turn_number))

      {:ok, :no_active_run} ->
        socket
    end
  end

  defp redispatch_unresolved_tool_calls(socket), do: socket

  defp redispatch_unresolved_tool_call(socket, tool_call, turn_number) do
    case mcp_tool_request_pending?(socket, tool_call.tool_call_id) do
      true ->
        socket

      false ->
        {:noreply, socket} = handle_interaction(tool_call, turn_number, socket)
        socket
    end
  end

  defp mcp_tool_request_pending?(socket, tool_call_id) do
    socket.assigns.pending_mcp_tool_requests
    |> Map.values()
    |> Enum.member?(tool_call_id)
  end

  defp route_to_mcp(tool_call, socket) do
    task_id = socket.assigns.task_id
    request_id = System.unique_integer([:positive])

    request =
      MCP.build_tool_execution(%MCP.ToolCallParams{
        request_id: request_id,
        tool_name: tool_call.tool_name,
        arguments: tool_call.arguments,
        call_id: tool_call.tool_call_id
      })

    in_progress_notification =
      ACP.tool_call_update(task_id, tool_call.tool_call_id, ACP.tool_call_status_in_progress())

    push(socket, @acp_message, in_progress_notification)

    socket = remember_mcp_tool_request(socket, request_id, tool_call.tool_call_id)

    push(socket, "mcp:message", request)
    {:noreply, socket}
  end

  defp remember_mcp_tool_request(socket, request_id, tool_call_id) do
    pending = Map.put(socket.assigns.pending_mcp_tool_requests, request_id, tool_call_id)
    assign(socket, :pending_mcp_tool_requests, pending)
  end

  defp to_plan_entry(%Todo{} = todo) do
    %{
      "content" => todo.content,
      "priority" => Atom.to_string(todo.priority),
      "status" => Atom.to_string(todo.status)
    }
  end

  defp push_current_todo_plan(socket) do
    {:ok, todos} = Tasks.list_todos(socket.assigns.scope, socket.assigns.task_id)
    push_current_todo_plan(socket, todos)
  end

  defp push_current_todo_plan(socket, todos) when is_list(todos) do
    entries = Enum.map(todos, &to_plan_entry/1)
    push(socket, @acp_message, ACP.plan_update(socket.assigns.task_id, entries))
  end
end
