# Frontman Server
# Copyright (C) 2025 Frontman AI
#
# Licensed under the AGPL-3.0 - see LICENSE for details.
# Additional terms apply - see AI-SUPPLEMENTARY-TERMS.md

defmodule AgentClientProtocol.History do
  @moduledoc "Encodes projected task row contexts as ACP notifications."

  alias AgentClientProtocol, as: ACP
  alias FrontmanServer.Agents
  alias FrontmanServer.Tasks.History, as: TaskHistory
  alias FrontmanServer.Tasks.Interaction
  alias FrontmanServer.Tasks.InteractionSchema

  @ignored_types [
    :turn_started,
    :agent_completed,
    :agent_retry,
    :message_edited,
    :discovered_project_rule,
    :discovered_project_structure
  ]

  def build(%TaskHistory{} = history, session_id, active_agents)
      when is_binary(session_id) and is_list(active_agents) do
    {rows, agent_ids} = TaskHistory.attributed_rows!(history)
    Agents.resolve_catalog!(active_agents, agent_ids)

    standalone_tool_call_ids =
      for %{
            row: %InteractionSchema{
              turn_number: turn_number,
              data: %Interaction.ToolCall{tool_call_id: id}
            }
          } <- rows,
          into: MapSet.new(),
          do: {turn_number, id}

    notifications =
      Enum.flat_map(rows, &encode_row(&1, session_id, standalone_tool_call_ids))

    {:ok, %{notifications: notifications}}
  end

  def encode_row(context, session_id), do: encode_row(context, session_id, MapSet.new())

  defp encode_row(
         %{
           row: %InteractionSchema{
             turn_number: turn_number,
             data: %Interaction.AgentResponse{} = response
           },
           turn_row: turn_row,
           agent_id: agent_id,
           response_ordinal: ordinal
         },
         session_id,
         standalone_tool_call_ids
       ) do
    content_notifications =
      case response.content do
        content when content in [nil, ""] ->
          []

        content when is_binary(content) ->
          [
            ACP.build_agent_message_chunk_notification(
              session_id,
              content,
              response.timestamp,
              ACP.agent_message_id(turn_row.id, ordinal),
              agent_id
            )
          ]
      end

    tool_call_notifications =
      response.metadata
      |> Map.get("tool_calls")
      |> Interaction.to_swarm_tool_calls()
      |> Enum.reject(&MapSet.member?(standalone_tool_call_ids, {turn_number, &1.id}))
      |> Enum.map(fn tool_call ->
        arguments =
          case Interaction.ToolCall.attrs(tool_call) do
            {:ok, %{arguments: arguments}} -> arguments
            {:error, {:invalid_tool_arguments, _message}} -> nil
          end

        ACP.tool_call_create(
          session_id,
          tool_call.id,
          tool_call.name,
          "other",
          response.timestamp,
          ACP.tool_call_status_pending(),
          arguments
        )
      end)

    content_notifications ++ tool_call_notifications
  end

  defp encode_row(
         %{row: %InteractionSchema{data: data, type: type} = row} = context,
         session_id,
         _standalone_tool_call_ids
       ) do
    case data do
      %Interaction.UserMessage{} = message ->
        message
        |> ACP.Content.from_user_message()
        |> Enum.map(fn content ->
          ACP.build_user_message_chunk_notification(
            session_id,
            row.id,
            content,
            context.agent_id,
            message.timestamp
          )
        end)

      %Interaction.ToolCall{} = call ->
        [
          ACP.tool_call_create(
            session_id,
            call.tool_call_id,
            call.tool_name,
            "other",
            call.timestamp,
            ACP.tool_call_status_pending(),
            call.arguments
          )
        ]

      %Interaction.ToolResult{} = result ->
        [
          ACP.tool_call_update(
            session_id,
            result.tool_call_id,
            ACP.tool_call_status(result.is_error),
            ACP.Content.from_tool_result(result.result),
            nil,
            result.result["structuredContent"]
          )
        ]

      %Interaction.AgentError{} = error ->
        [
          ACP.build_error_notification(session_id, error.error, error.timestamp,
            category: error.category,
            agent_error_id: error.id
          )
        ]

      %Interaction.AgentPaused{} ->
        [ACP.build_state_update_notification(session_id, "requires_action")]

      _ignored when type in @ignored_types ->
        []

      _unsupported ->
        raise FunctionClauseError, module: __MODULE__, function: :encode_row, arity: 2
    end
  end
end
